#include<"Utils.txt">;
#include<"Bass.txt">;

Main{
    main()->N:={
        WinMain(GetModuleHandleW(0),0,0,1);
    }
}

Window{
	N:hBTOpen;
	N:hBTPlay;
	N:hBTStop;
	N:hEDFile;
	N:hStream;
    WndProc(N:hWnd,N:uMsg,N:wParam,N:lParam)->N:={
		PAINTSTRUCT:ps;
		RECT:rcClient;

		Block()
			case(uMsg==Cst.WM_COMMAND){
				if(HIWORD(wParam)==0){
					Block()
						case(lParam==hBTOpen)
						{
							OPENFILENAMEW:ofn;
							ofn.lStructSize=88;
							ofn.hwndOwner=hWnd;
							ofn.hInstance=0;
							B:szFilter[200];
							
							N:p = &szFilter;
							p = AssignStr(p, "音频文件");
							p = AssignStr(p, ".mp1;*.mp2;*.xm;*.mp3;*.flac;*.wma;*.wav;*.m4a;*.ogg;*.acc;*.ape;*.aiff");
							p = AssignWORD(p, 0);
							
							ofn.lpstrFilter = &szFilter;
							ofn.lpstrCustomFilter = 0;
							ofn.nMaxCustFilter = 0;
							ofn.nFilterIndex = 1;
							
							B:szFile[512];
							ofn.lpstrFile = &szFile;
							AssignWORD(&szFile, 0);
							
							ofn.nMaxFile = 256;
							ofn.lpstrFileTitle = 0;
							ofn.nMaxFileTitle = 0;
							ofn.lpstrInitialDir = 0;
							ofn.lpstrTitle = 0;
							ofn.Flags = 0;
							AssignWORD(&ofn.nFileOffset,0);
							AssignWORD(&ofn.nFileExtension,0);
							ofn.lpstrDefExt = 0;
							ofn.lCustData = 0;
							ofn.lpfnHook = 0;
							ofn.lpTemplateName = 0;
							ofn.pvReserved = 0;
							ofn.dwReserved = 0;
							ofn.FlagsEx = 0;
							
							if(GetOpenFileNameW(ofn) != 0)
							{
								SetWindowTextW(hEDFile, &szFile);
							};
							return(0);
						}
						case(lParam==hBTPlay)
						{
							if(hStream != 0)
							{
								BASS_StreamFree(hStream);
							};
							B:szFile[512];
							GetWindowTextW(hEDFile, &szFile, 256);
							hStream = BASS_StreamCreateFile(0, &szFile, 0, 0, 0, 0, 2147483648);
							BASS_ChannelPlay(hStream, 1);
							return(0);
						}
						case(lParam==hBTStop)
						{
							if(hStream != 0)
							{
								BASS_StreamFree(hStream);
								hStream = 0;
							};
						};
				};
			}
			
			case(uMsg==Cst.WM_DESTROY){
				PostQuitMessage(0);
				return(0)
			}
			case(uMsg==Cst.WM_CREATE)
			{
				Z:b=BASS_Init(-1,44100,0,hWnd,0);
				if(b==0)
				{
					MessageBoxW(0,"error","Bass初始化失败！",0);
				};
				hBTOpen=CreateWindowExW(0,"Button","打开...",
					Cst.WS_VISIBLE or Cst.WS_CHILD,
					20,120,120,30,hWnd,0,0,0);
				hBTPlay=CreateWindowExW(0,"Button","播放",
					Cst.WS_VISIBLE or Cst.WS_CHILD,
					160,120,120,30,hWnd,0,0,0);
				hBTStop=CreateWindowExW(0,"Button","停止",
					Cst.WS_VISIBLE or Cst.WS_CHILD,
					300,120,120,30,hWnd,0,0,0);
				hEDFile=CreateWindowExW(Cst.WS_EX_CLIENTEDGE,"Edit",0,
					Cst.WS_VISIBLE or Cst.WS_CHILD or Cst.ES_AUTOHSCROLL,
					20,50,400,30,hWnd,0,0,0);
				return(0);
			};
		
        return(DefWindowProcW(hWnd,uMsg,wParam,lParam))
    }
	
    WinMain(N:hInstance,N:hPrevInstance,N:szCmdLine,N:nCmdShow)->N:={
        N:hWnd;
		MSG:msg;
		WNDCLASS:wc;
		N:szCls = "MLang.Player.WndClass.Main";

        wc.style=3;
        wc.lpfnwndproc=~WndProc;
        wc.cbClsextra=0;
        wc.cbWndExtra=0;
        wc.hInstance=hInstance;
        wc.hIcon=LoadIconW(0,"32512");
        wc.hCursor=LoadCursorW(0,"32512");
        wc.hbrBackground=GetStockObject(0);
        wc.lpszMenuName=&"";
        wc.lpszClassName=szCls;
        if(RegisterClassW(wc)==0)
		{
            MessageBoxW(0,"error","注册窗口类失败！",0);
			return(0);
        };
        hWnd=CreateWindowExW(
			0,
			szCls,
			"爱慕浪运动员",
			Cst.WS_OVERLAPPEDWINDOW,
			Cst.CW_USEDEFAULT,
			Cst.CW_USEDEFAULT,
			460,
			210,
			0,0,hInstance,0);

        ShowWindow(hWnd,nCmdShow);
        UpdateWindow(hWnd);
        while(GetMessageW(msg,0,0,0)!=0)
		{
            TranslateMessage(msg);
            DispatchMessageW(msg);
        };
		BASS_Free();
	
        return(msg.wParam);
    }
}